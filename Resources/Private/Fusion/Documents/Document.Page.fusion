// TODO 9.0 migration: "Neos.Neos:PrimaryContent" has been removed without a complete replacement. We replaced all usages with "Neos.Neos:ContentCollection" but not the prototype definition. Please check the replacements and if you have overridden the "Neos.Neos:PrimaryContent" prototype and rewrite it for your needs.
prototype(Networkteam.Neos.Mockup:Page) < prototype(Neos.Neos:Page) {
    head {
        # Link tags for stylesheets in the head should go here
        stylesheets {
            mockup = Neos.Fusion:Tag {
                tagName = 'link'
                omitClosingTag = true
                attributes {
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://Networkteam.Neos.Mockup/Public/Css/mockup.css'
                    }
                    rel = "stylesheet"
                }
            }
        }
    }

    body = Networkteam.Neos.Mockup:Components.Templates.Page {
        header = Networkteam.Neos.Mockup:Components.Molecules.Header {
            mainNavigation = Networkteam.Neos.Mockup:Components.Organisms.Navbar {
                items = Networkteam.Neos.Mockup:Document.Menu {
                    startingPoint = ${site}
                }
            }
        }

        content =  Neos.Fusion:Join {
            // The default content section
            main = Neos.Neos:ContentCollection {
                nodePath = 'main'
            }
        }

        footer = null
    }

    bodyScripts = Networkteam.Neos.Mockup:Document.Body.Scripts {
        @position = 'before closingBodyTag'
    }
}
