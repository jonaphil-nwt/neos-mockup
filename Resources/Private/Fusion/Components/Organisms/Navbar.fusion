prototype(Networkteam.Neos.Mockup:Components.Organisms.Navbar) < prototype(Neos.Fusion:Component) {
    items = ${[]}
    title = ''

    renderer = afx`
        <div id="mainnav" class="navbar navbar-inverse">
            <div class="container">
                <div class="navbar-header">
                    <button type="button" class="navbar-toggle collapsed" data-toggle="collapse" data-target="#mainNavigation">
                        <span class="sr-only">Toggle navigation</span>
                        <span class="icon-bar"></span>
                        <span class="icon-bar"></span>
                        <span class="icon-bar"></span>
                    </button>
                    <a class="navbar-brand" href="/">{props.title}</a>
                </div>
                <div class="collapse navbar-collapse" id="mainNavigation">
                    <ul class='nav navbar-nav'>
                        <Networkteam.Neos.Mockup:Components.Organisms.Navbar.ItemList items={props.items} />
                    </ul>
                </div>
            </div>
        </div>
    `
}

prototype(Networkteam.Neos.Mockup:Components.Organisms.Navbar.ItemList) < prototype(Neos.Fusion:Component) {
    items = ${[]}

    renderer = afx`
        <Neos.Fusion:Loop items={props.items} @children='itemRenderer'>
            <Networkteam.Neos.Mockup:Components.Organisms.Navbar.ItemList.Item
                label={item.label}
                state={item.state}
                link={item.link}
                subItems={item.subItems}
            />
        </Neos.Fusion:Loop>
    `
}

prototype(Networkteam.Neos.Mockup:Components.Organisms.Navbar.ItemList.Item) < prototype(Neos.Fusion:Component) {
    label = ''
    state = ''
    link = ''
    subitems = ${[]}
    identifier = ''

    renderer = afx`
        <li class={props.state + (props.subItems ? ' dropdown-submenu' : '')}>
            <a href={props.link} class="dropdown-toggle" data-toggle="dropdown">
                {props.label}
                <span @if.hasSubitems={props.subItems} class="caret"></span>
            </a>
            <ul @if.hasSubitems={props.subItems} class="dropdown-menu">
                <Networkteam.Neos.Mockup:Components.Organisms.Navbar.ItemList
                    items={props.subItems}
                    menuLevel={props.menuLevel + 1}
                />
            </ul>
        </li>
    `
}